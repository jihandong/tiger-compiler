%{

/********************************************************************************
 * Includes
 ********************************************************************************/

#include <stdio.h>
#include <stdlib.h>
#include "ast.h"
#include "symbol.h"

/********************************************************************************
 * External
 ********************************************************************************/

A_exp A_root;

extern FILE *yyin;
int yyparse(void);
int yylex(void);

/********************************************************************************
 * Public
 ********************************************************************************/

void yyerror(char *s)
{
    fprintf(stderr, "Parse error: \"%s\"\n", s);
}

int parse(const char *filename)
{
    int ret;

    yyin = fopen(filename, "r");
    if (!yyin) {
        fprintf(stderr, "cannot open %s\n", filename);
        exit(1);
    }

    A_root = NULL;
    ret = yyparse();

    fclose(yyin);
    return ret;
}

/********************************************************************************
 * End
 ********************************************************************************/

%}

%union
{
    int                         pos;
    int                         ival;
    const char *                sval;
    A_dec                     dec;
    A_exp                     exp;
    A_var                     var;
    A_type                    type;
    A_dec_list                decs;
    A_exp_list                exps;
    A_para                  para;
    A_para_list             paras;
    A_argu                  argu;
    A_argu_list             argus;
}

%token <ival>   INT
%token <sval>   ID STRING
%token          COMMA COLON SEMICOLON LPAREN RPAREN LBRACK RBRACK LBRACE RBRACE
                DOT PLUS MINUS TIMES DIVIDE EQ NEQ LT LE GT GE AND OR ASSIGN
                ARRAY IF THEN ELSE WHILE FOR TO DO LET IN END OF BREAK NIL
                FUNCTION VAR TYPE

%nonassoc   ASSIGN
%left       AND OR
%nonassoc   EQ NEQ LT LE GT GE
%left       PLUS MINUS
%left       TIMES DIVIDE
%nonassoc   UMINUS

%type <dec>     dec dec_var dec_type dec_func
%type <exp>     program exp exp_value exp_seq exp_op exp_call exp_create
                exp_assign exp_if exp_for exp_while exp_break exp_let
%type <var>     lvalue lvalue_suffix
%type <type>    type
%type <decs>    decs
%type <exps>    seqfs argfs
%type <para>  para
%type <paras> paras
%type <argu>  argu
%type <argus> argus

%start program

%%

program: exp { A_root = $1; }

/****************************************************************************
 * declares
 ****************************************************************************/

decs
: /* epsilon */     { $$ = NULL; }
| dec decs          { $$ = Am_dec_list($1, $2); }

dec
: dec_var   { printf("declaration(var)\n"); $$ = $1; }
| dec_type  { printf("declaration(type)\n"); $$ = $1; }
| dec_func  { printf("declaration(func)\n"); $$ = $1; }

dec_var
: VAR ID ASSIGN exp             { $$ = Am_dec_var(0, Sf_symbol($2), NULL, $4); }
| VAR ID COLON ID ASSIGN exp    { $$ = Am_dec_var(0, Sf_symbol($2), Sf_symbol($4), $6); }

dec_type
: TYPE ID EQ type   { $$ = Am_dec_type(Sf_symbol($2), $4); }

dec_func
: FUNCTION ID LPAREN RPAREN EQ exp                  { $$ = Am_dec_func(0, Sf_symbol($2), NULL, NULL, $6); }
| FUNCTION ID LPAREN RPAREN COLON ID EQ exp         { $$ = Am_dec_func(0, Sf_symbol($2), NULL, Sf_symbol($6), $8); }
| FUNCTION ID LPAREN paras RPAREN EQ exp           { $$ = Am_dec_func(0, Sf_symbol($2), $4, NULL, $7); }
| FUNCTION ID LPAREN paras RPAREN COLON ID EQ exp  { $$ = Am_dec_func(0, Sf_symbol($2), $4, Sf_symbol($7), $9); }

/****************************************************************************
 * expressions
 ****************************************************************************/

exp
: exp_value     { printf("expression(value)\n");    $$ = $1; }
| exp_seq       { printf("expression(seq)\n");      $$ = $1; }
| exp_op        { printf("expression(op)\n");       $$ = $1; }
| exp_call      { printf("expression(call)\n");     $$ = $1; }
| exp_create    { printf("expression(create)\n");   $$ = $1; }
| exp_assign    { printf("expression(assign)\n");   $$ = $1; }
| exp_if        { printf("expression(if)\n");       $$ = $1; }
| exp_for       { printf("expression(for)\n");      $$ = $1; }
| exp_while     { printf("expression(while)\n");    $$ = $1; }
| exp_break     { printf("expression(break)\n");    $$ = $1; }
| exp_let       { printf("expression(let)\n");      $$ = $1; }
| error         { ; }

exp_value
: NIL       { $$ = Am_exp_nil(0); }
| INT       { $$ = Am_exp_int(0, $1); }
| STRING    { $$ = Am_exp_string(0, $1); }
| lvalue    { $$ = Am_exp_var(0, $1); }

exp_seq
: LPAREN seqfs RPAREN { $$ = Am_exp_seq(0, $2); }

exp_op
: exp PLUS exp              { $$ = Am_exp_op(0, Ak_op_plus,   $1, $3); }
| exp MINUS exp             { $$ = Am_exp_op(0, Ak_op_minus,  $1, $3); }
| exp TIMES exp             { $$ = Am_exp_op(0, Ak_op_times,  $1, $3); }
| exp DIVIDE exp            { $$ = Am_exp_op(0, Ak_op_divide, $1, $3); }
| exp EQ exp                { $$ = Am_exp_op(0, Ak_op_eq,     $1, $3); }
| exp NEQ exp               { $$ = Am_exp_op(0, Ak_op_neq,    $1, $3); }
| exp LT exp                { $$ = Am_exp_op(0, Ak_op_lt,     $1, $3); }
| exp LE exp                { $$ = Am_exp_op(0, Ak_op_le,     $1, $3); }
| exp GT exp                { $$ = Am_exp_op(0, Ak_op_gt,     $1, $3); }
| exp GE exp                { $$ = Am_exp_op(0, Ak_op_ge,     $1, $3); }
| MINUS exp %prec UMINUS    { $$ = Am_exp_op(0, Ak_op_minus,  Am_exp_int(0, 0), $2); }

exp_call
: ID LPAREN RPAREN          { $$ = Am_exp_call(0, Sf_symbol($1), NULL); }
| ID LPAREN argfs RPAREN    { $$ = Am_exp_call(0, Sf_symbol($1), $3); }

exp_create
: ID LBRACE RBRACE              { $$ = Am_exp_record(0, Sf_symbol($1), NULL); }
| ID LBRACE argus RBRACE     { $$ = Am_exp_record(0, Sf_symbol($1), $3); }
| ID LBRACK exp RBRACK OF exp   { $$ = Am_exp_array(0, Sf_symbol($1), $3, $6); }

exp_assign
: lvalue ASSIGN exp { $$ = Am_exp_assign(0, $1, $3); }

exp_if
: IF exp THEN exp ELSE exp  { $$ = Am_exp_if(0, $2, $4, $6); }
| IF exp THEN exp           { $$ = Am_exp_if(0, $2, $4, NULL); }
| exp AND exp               { $$ = Am_exp_if(0, $1, $3, Am_exp_int(0, 0)); }
| exp OR exp                { $$ = Am_exp_if(0, $1, Am_exp_int(0, 1), $3); }

exp_for
: FOR ID ASSIGN exp TO exp DO exp { $$ = Am_exp_for(0, Sf_symbol($2), $4, $6, $8); }

exp_while
: WHILE exp DO exp { $$ = Am_exp_while(0, $2, $4); }

exp_break
: BREAK { $$ = Am_exp_break(0); }

exp_let
: LET decs IN seqfs END { $$ = Am_exp_let(0, $2, $4); }

/****************************************************************************
 * variables
 ****************************************************************************/

lvalue
: ID                { $$ = Am_var(0, Sf_symbol($1), NULL); }
| ID lvalue_suffix  { $$ = Am_var(0, Sf_symbol($1), $2); }

lvalue_suffix
: /* spsilon */                     { $$ = NULL; }
| LBRACK exp RBRACK lvalue_suffix   { $$ = Am_var_slice(0, $2, $4); }
| DOT ID lvalue_suffix              { $$ = Am_var_member(0, Sf_symbol($2), $3); }

/****************************************************************************
 * types
 ****************************************************************************/

type
: ID                    { printf("type(simple)\n"); $$ = Am_type_var(0, Sf_symbol($1)); }
| ARRAY OF ID           { printf("type(array)\n");  $$ = Am_type_array(0, Sf_symbol($3)); }
| LBRACE paras RBRACE  { printf("type(record)\n"); $$ = Am_type_record(0, $2); }
| LBRACE RBRACE         { printf("type(record)\n"); $$ = Am_type_record(0, NULL); }

/****************************************************************************
 * link list
 ****************************************************************************/

paras
: para                 { $$ = Am_para_list($1, NULL); }
| para COMMA paras    { $$ = Am_para_list($1, $3); }

para
: ID COLON ID   { $$ = Am_para(0, Sf_symbol($1), Sf_symbol($3)); }

argus
: argu                   { $$ = Am_argu_list($1, NULL); }
| argu COMMA argus    { $$ = Am_argu_list($1, $3); }

argu
: ID EQ exp { $$ = Am_argu(Sf_symbol($1), $3); }

seqfs
: exp                   { $$ = Am_exp_list($1, NULL); }
| exp SEMICOLON seqfs   { $$ = Am_exp_list($1, $3); }

argfs
: exp               { $$ = Am_exp_list($1, NULL); }
| exp COMMA argfs   { $$ = Am_exp_list($1, $3); }

